
plugins {
    id "net.neoforged.gradle.userdev" version "$neogradle_version"
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

base {
    archivesName = "${mod_id}-earlywindow"
}

repositories {
    mavenCentral()
    maven {
        name = 'NeoForge'
        url = 'https://maven.neoforged.net/releases/'
    }
    maven {
        name = 'Shadow'
        url = 'https://plugins.gradle.org/m2/'
        content {
            includeGroup 'gradle.plugin.com.github.jengelman.gradle.plugins'
        }
    }
}

dependencies {
    compileOnly "net.neoforged:neoforge:${neoforge_version}"
    implementation "com.github.Keksuccino:japng:0.5.3"
}

tasks.named("jar", Jar).configure {
    archiveClassifier.set("plain")
}

tasks.named("shadowJar").configure {
    archiveClassifier.set("")
    manifest.inheritFrom(tasks.named("jar").get().manifest)
    from(rootProject.file("LICENSE")) {
        rename { "${it}_${mod_name}" }
    }
}

tasks.named("build").configure {
    dependsOn(tasks.named("shadowJar"))
}
