
plugins {
    id 'java-library'
    id 'com.gradleup.shadow' version '9.2.2'
}

base {
    archivesName = "${mod_id}-earlywindow"
}

repositories {
    mavenCentral()
    maven {
        name = 'NeoForge'
        url = 'https://maven.neoforged.net/releases/'
    }
    maven {
        name = 'Mojang'
        url = 'https://libraries.minecraft.net'
        content {
            includeGroup 'com.mojang'
        }
    }
    maven {
        name = 'Shadow'
        url = 'https://plugins.gradle.org/m2/'
        content {
            includeGroup 'gradle.plugin.com.github.jengelman.gradle.plugins'
            includeGroup 'com.gradleup'
        }
    }
}

def lwjglVersion = '3.3.3'

dependencies {
//    compileOnly "net.neoforged:neoforge:${neoforge_version}"
//    compileOnly "net.neoforged.fancymodloader:loader:4.0.24"
    compileOnly "org.lwjgl:lwjgl-glfw:${lwjglVersion}"
    compileOnly "org.lwjgl:lwjgl-opengl:${lwjglVersion}"
    compileOnly "org.lwjgl:lwjgl-stb:${lwjglVersion}"
    compileOnly "org.lwjgl:lwjgl-jemalloc:${lwjglVersion}"
    compileOnly "org.lwjgl:lwjgl-tinyfd:${lwjglVersion}"
    implementation "com.github.Keksuccino:japng:0.5.3"
}

tasks.named("jar", Jar).configure {
    archiveClassifier.set("plain")
}

tasks.named("shadowJar").configure {
    archiveClassifier.set("")
    from(rootProject.file("LICENSE")) {
        rename { "${it}_${mod_name}" }
    }
    manifest {
        attributes([
                'Specification-Title'     : mod_name,
                'Specification-Vendor'    : mod_authors,
                'Specification-Version'   : mod_version,
                'Implementation-Title'    : mod_name,
                'Implementation-Version'  : mod_version,
                'Implementation-Vendor'   : mod_authors,
                'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                'Built-On-Minecraft'      : minecraft_version
        ])
    }
    relocate('net.ellerton.japng', 'de.keksuccino.drippyloadingscreen.earlywindow.shadow.japng')
}

tasks.named("build").configure {
    dependsOn(tasks.named("shadowJar"))
}
